/**
 * Used when working with this plugin.
 */
plugins {
	id "groovy-gradle-plugin"
	id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
	id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "2.2.1"
	id "com.diffplug.spotless" version "6.25.0"
}

/**
 * Dependencies needed by the plugin in src/main/groovy
 */
dependencies {
	implementation "com.gradleup.shadow:shadow-gradle-plugin:8.3.2"
	implementation "io.github.gradle-nexus:publish-plugin:2.0.0"
	implementation "com.gradle.publish:plugin-publish-plugin:1.3.0"
	implementation "se.bjurr.gitchangelog:git-changelog-gradle-plugin:2.2.1"
}

repositories {
	gradlePluginPortal()
	mavenCentral()
	mavenLocal()
}

sourceCompatibility = 17
targetCompatibility = 17

spotless {
	groovyGradle {
		target '*.gradle', '**/*.gradle'
		greclipse()
	}
}
processResources.dependsOn spotlessApply

apply from: "src/main/groovy/se.bjurr.gradle.conventional-release.gradle"

/**
 * Update README with config
 */
def defaultConfig = file('src/main/groovy/se.bjurr.gradle.conventional-release.gradle')
		.text
		.takeBetween('// ---- default config ----')
		.stripIndent()

def newReadme = file('README.md')
		.text
		.replaceAll("(?s)(.*// ---- default config ----).*(// ---- default config ----.*)", "\$1$defaultConfig\$2")
file('README.md').write(newReadme)