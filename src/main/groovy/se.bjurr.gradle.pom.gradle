import org.gradle.api.model.ObjectFactory
import org.gradle.api.provider.Property
import org.gradle.api.provider.ListProperty

class PomExtension {
	final Property<String> website
	final Property<String> vcsUrl
	final Property<String> licenseName
	final Property<String> licenseUrl
	final ListProperty<Map<String, String>> developers

	PomExtension(ObjectFactory objects, Project project) {
		website = objects.property(String)
		vcsUrl = objects.property(String)
		licenseName = objects.property(String)
		licenseUrl = objects.property(String)
		developers = objects.listProperty(Map)

		def gitUrl = project.provider {
			try {
				def process = [
					'git',
					'config',
					'--get',
					'remote.origin.url'
				].execute()
				process.waitFor()
				def url = process.text.trim()
				return url ?: "https://github.com/tomasbjerre/${project.name}"
			} catch (Exception e) {
				project.logger.warn("Could not get git remote URL: " + e.getMessage())
				return "https://github.com/tomasbjerre/${project.name}"
			}
		}
		def websiteUrl = gitUrl.map { url ->
			if (!url) {
				return url
			}
			if (url.startsWith('git@')) {
				url = url.replaceFirst(':', '/')
						.replaceFirst('git@', 'https://')
			}
			if (url.endsWith('.git')) {
				url = url.substring(0, url.length() - 4)
			}
			return url
		}

		website.convention(websiteUrl)
		vcsUrl.convention(gitUrl)
		licenseName.convention("The Apache Software License, Version 2.0")
		licenseUrl.convention("http://www.apache.org/licenses/LICENSE-2.0.txt")
		developers.convention([
			[
				id: "tomasbjerre",
				name: "Tomas Bjerre",
				email: "tomas.bjerre85@gmail.com"
			]
		])
	}
}

project.extensions.create('pomExtension', PomExtension, project.objects)

import org.gradle.api.publish.maven.tasks.GenerateMavenPom

tasks.withType(GenerateMavenPom).configureEach { task ->
	doFirst {
		task.pom.description = project.provider {
			project.description?.replaceAll("[\"']", "")
		}.get()

		task.pom.name = project.name

		task.pom.withXml {
			def root = asNode()
			def websiteUrl = project.extensions.pomExtension.website.get();
			def vcsUrl = project.extensions.pomExtension.vcsUrl.get();
			logger.info("Using websiteUrl ${websiteUrl}")
			logger.info("Using vcsUrl ${vcsUrl}")
			root.appendNode('url', websiteUrl)
			root.appendNode('inceptionYear', (1900 + new Date().year).toString())

			root.children().last() + {
				scm {
					url websiteUrl
					connection vcsUrl
					developerConnection vcsUrl
				}

				licenses {
					license {
						name project.extensions.pomExtension.licenseName.get()
						url project.extensions.pomExtension.licenseUrl.get()
						distribution 'repo'
					}
				}

				developers {
					project.extensions.pomExtension.developers.get().each { dev ->
						developer {
							id dev.id
							name dev.name
							email dev.email
						}
					}
				}
			}
		}
	}
}